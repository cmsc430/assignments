#lang racket
(provide interp)
(require "../syntax/ast.rkt")
(require "interp-prim.rkt")

;; type Value =
;; | Integer
;; | Boolean
;; | Character
;; | Eof
;; | Void
;; Expr -> Value
(define (interp e)
  (match e
    [(Lit d) d]
    [(Eof)   eof]
    [(Prim0 p)
     (interp-prim0 p)]
    [(Prim1 p e)
     (interp-prim1 p (interp e))]
    [(If e1 e2 e3)
     (if (interp e1)
         (interp e2)
         (interp e3))]
    [(Begin e1 e2)
     (begin (interp e1)
            (interp e2))]
    [(Until e1 e2)
     (interp-until e1 e2 (void))]))

;; Expr Expr Value -> Value
(define (interp-until e1 e2 result-value)
  (if (interp e1)
      result-value
      (let ((v (interp e2)))
        (interp-until e1 e2 v))))
